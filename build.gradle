plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.7'
	id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
    id 'application'
}

group = 'com.inditex'
version = '0.0.1-SNAPSHOT'
description = 'Inditex Coding Challenge Sheila Ros'

application {
    mainClass = 'com.inditex.pricing.PricingApplication'
}

jacoco {
    toolVersion = "0.8.13"
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.liquibase:liquibase-core'

    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.h2database:h2'

    implementation("org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0")

    // TEST
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

    testImplementation "io.karatelabs:karate-junit5:1.5.1"
    testImplementation platform("io.cucumber:cucumber-bom:7.15.0")
    testImplementation "io.cucumber:cucumber-java"
    testImplementation "io.cucumber:cucumber-junit-platform-engine"
    testImplementation "org.wiremock:wiremock-standalone:3.6.0"
    testCompileOnly 'org.projectlombok:lombok:1.18.32'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.32'
}

tasks.named('test') {
	useJUnitPlatform()
    exclude '**/karate/**'
    testLogging {
        events "passed", "skipped", "failed"
    }

    finalizedBy jacocoTestReport
}


jacocoTestReport {
    dependsOn test
    reports {
        html.required = true
    }

    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, exclude: [
                            '**/*Config*.class',
                            '**/*Application*.class',
                            '**/*Advice*.class'
                    ])
                })
        )
    }
}

sourceSets {
    test {
        resources {
            srcDirs 'src/test/resources', 'src/test/java'
            include '**/*.feature', '**/*.json', '**/*.xml'
        }
    }
}

tasks.register('karate', Test) {
    useJUnitPlatform()
    include '**/KarateTestsRunner.class'

    systemProperty 'karate.options', '--reportdir build/karate-reports --tags ~@ignore'
    testLogging {
        events "passed", "failed", "skipped"
        showStandardStreams = true
    }

    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\n==== Karate Summary ===="
            println "Tests: ${result.testCount}, Passed: ${result.successfulTestCount}, Failed: ${result.failedTestCount}, Skipped: ${result.skippedTestCount}"
            println "Report: build/karate-reports/karate-summary.html"
            println "========================\n"
        }
    }

    testLogging {
        showStandardStreams = true
    }
    systemProperty 'karate.logPrettyRequest', 'true'
    systemProperty 'karate.logPrettyResponse', 'true'
    systemProperty 'karate.configure', "{ report: { showLog: true, showAllSteps: true } }"
}